apply plugin: 'com.android.library'

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    // Check AGP version for backward compatibility reasons (for RN < 0.71)
    if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
        namespace "com.eko"
    }

    compileSdkVersion safeExtGet('compileSdkVersion', 34)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 34)
        versionCode 1
        versionName '1.0'
    }

    sourceSets {
        main {
            java {
                if (isNewArchitectureEnabled()) {
                    srcDirs += ["build/generated/source/codegen/java"]
                }
            }
        }
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'
    implementation 'io.github.zhongwuzw:mmkv:2.2.4'
    implementation 'com.google.code.gson:gson:2.11.0'
}
